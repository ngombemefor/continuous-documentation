buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'org.asciidoctor:asciidoctorj-screenshot:0.1.1'
    }
}

plugins {
    id "de.undercouch.download" version "2.1.0"
}

// Apply the java plugin to add support for Java
apply plugin: 'war'

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = 'org.todo'
    version = '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

dependencies {
    providedCompile 'javax:javaee-api:7.0'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

apply plugin: 'org.asciidoctor.gradle.asciidoctor'

asciidoctor {
    sourceDir = file('src/main/asciidoc')
    attributes = [
            'rootdir': "${rootDir}",
            'application-url': 'http://192.168.99.100/'
    ]
}

task generateManual {
    dependsOn 'generateJaxRsDocumentation'
    finalizedBy asciidoctor
}


task downloadFile(type: de.undercouch.gradle.tasks.download.Download) {
    src 'https://github.com/sdaschner/jaxrs-analyzer/releases/download/v0.9/jaxrs-analyzer.jar'
    dest buildDir
}

task generateJaxRsDocumentation(type:Exec) {
    dependsOn compileJava

    if (!new File("${buildDir}/jaxrs-analyzer.jar").exists()) {
        dependsOn downloadFile
    }

    workingDir "${buildDir}"

    commandLine 'java',  '-jar',  'jaxrs-analyzer.jar',
            '-b',  'asciidoc', '-o' , "${buildDir}/todorest.adoc" , "${buildDir}/classes/main"

}